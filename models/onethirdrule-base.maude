load ./sorts/fvp-nat.maude
load ./sorts/sorts.maude
load ../heuristic-search/state.maude
load ../heuristic-search/invariant.maude

set include BOOL off .


mod CONFIGURATION is
    protecting NODE .
    protecting INITIAL-SORTS .

    sorts None Object Msg ObjectSet Configuration InitObject InitConfiguration .
    subsort InitObject < Object InitConfiguration < Configuration .
    subsort Msg < Configuration .

    op none : -> InitConfiguration [ctor] .
    op __ : InitConfiguration InitConfiguration -> InitConfiguration [ctor assoc comm id: none] .
    op __ : Configuration Configuration -> Configuration [ctor assoc comm id: none] .

    op <_:_|_> : Oid Cid IU1234S -> InitObject [ctor object] .
    op <_:_|_> : Oid Cid U1234S -> Object [ctor object] .
endm


fmod MESSAGE-CONTENT is
    protecting FVP-NAT .
    protecting SORTS .
    sort MsgContent . 

    op vote : Value Natural -> MsgContent [ctor] .
endfm



mod MULTICAST is 
  including CONFIGURATION .
  including MESSAGE-CONTENT .

  op multicast_from_to_ : MsgContent Oid OidSet -> Msg [ctor] .
  var MC : MsgContent . var SENDER : Oid .
  eq multicast MC from SENDER to none = none [variant] .
  eq multicast MC from SENDER to none = none .
endm

mod GLOBAL is
    protecting CONFIGURATION .
    protecting FVP-NAT .
    including STATE .

    op {_,_,_} : Natural OidSet Configuration -> State [ctor] .
    op decide? :  Value Boolean Natural OidSet Object Configuration -> State [ctor] .
    op update? :  Value Boolean Natural OidSet Object Configuration -> State [ctor] .
endm


mod ONETHIRDRULE-BASE is
    including GLOBAL .
    protecting VALUE .
    protecting MULTICAST .
    protecting BOOL .
    protecting FVP-NAT .
    protecting FVP-BOOL .


    vars N N' VT VF VA : Natural .

    ops _<=_ _<_ : Natural Natural -> Boolean .
    eq N <= N N' = true [variant] .
    eq N < N' = s N <= N' [variant] .
    eq N <= N N' = true .
    eq N < N' = s N <= N' .

    op update : Vote Value -> Vote .
    eq update([VT, VF, VA], true) = [s VT, VF, s VA] [variant] .
    eq update([VT, VF, VA], false) = [VT, s VF, s VA] [variant] .
    eq update([VT, VF, VA], true) = [s VT, VF, s VA] .
    eq update([VT, VF, VA], false) = [VT, s VF, s VA] .
endm


mod INV-BASE is
    including ONETHIRDRULE-BASE .
    including INVARIANT .

    vars N N' R : Natural . var Z : Zero .  
    var S : Step .
    var CONF : Configuration . var M : Msg . 
    vars O O' : Oid . var C : Cid .  var OS : OidSet .

    op getOids : Configuration -> OidSet [memo] .
    eq getOids(< O : C | ATTS:U1234S > CONF) = O ; getOids(CONF) .
    eq getOids(M CONF) =  getOids(CONF) .
    eq getOids(none) =  none .

    op numObjs : Configuration -> Natural .
    eq numObjs(< O : Node | ATTS:U1234S > CONF) = s numObjs(CONF) .
    eq numObjs(M CONF) = numObjs(CONF) .
    eq numObjs(none) = 0 .

    op noDupl : OidSet -> Bool .
    eq noDupl(O ; O ; OS) = false .

    op noDupl : Configuration -> Bool .
    eq noDupl(< O : Node | ATTS:U1234S > < O : Node | ATTS':U1234S > CONF) = false .

    op sameRound : Configuration -> Bool .
    eq sameRound(< O : Node | status : [R, S], ATTS:U234S > < O' : Node | status : [s R N, S], ATTS':U234S > CONF) = false .

    ops isHalfThreshold isTwoThirdThreshold 
    : Natural Natural -> Bool .

    eq isHalfThreshold(s s N, s N') 
    = isHalfThreshold(N, N') .
    eq isHalfThreshold(N, Z) = false .

    eq isTwoThirdThreshold(s s s N, s s N') 
    = isTwoThirdThreshold(N, N') .
    eq isTwoThirdThreshold(s s N, Z) = false .
    eq isTwoThirdThreshold(s s N, s Z) = false .
    eq isTwoThirdThreshold(s N, Z) = false .
    eq isTwoThirdThreshold(N, Z) = false .
endm

mod INV-HALF is
    including INV-BASE .

    vars N : Natural .
    var OS : OidSet .
    var CONF : Configuration .

    eq inv(S:[State]) = true .

    eq initCond({N, OS, CONF})
    = noDupl(OS) 
    and noDupl(CONF) 
    and sameRound(CONF) 
    and isHalfThreshold(numObjs(CONF), N) .

endm

mod INV-TWOTHIRD is
    including INV-BASE .

    vars N : Natural .
    var OS : OidSet .
    var CONF : Configuration .

    eq inv(S:[State]) = true .

    eq initCond({N, OS, CONF})
    = noDupl(OS) 
    and noDupl(CONF) 
    and sameRound(CONF) 
    and isTwoThirdThreshold(numObjs(CONF), N) .
endm

mod INV-HALF-FIXED-INITIAL-ROUND is
    including INV-BASE .

    vars N : Natural .
    var OS : OidSet .
    var CONF : Configuration .

    eq inv(S:[State]) = true .

    eq initCond({N, OS, CONF})
    = noDupl(OS) 
    and noDupl(CONF) 
    and isHalfThreshold(numObjs(CONF), N) .

endm

mod INV-TWOTHIRD-FIXED-INITIAL-ROUND is
    including INV-BASE .

    vars N : Natural .
    var OS : OidSet .
    var CONF : Configuration .

    eq inv(S:[State]) = true .

    eq initCond({N, OS, CONF})
    = noDupl(OS) 
    and noDupl(CONF) 
    and isTwoThirdThreshold(numObjs(CONF), N) .
endm


mod INV-FIXED-OBJS is
    including ONETHIRDRULE-BASE .
    including INVARIANT .

    vars O : Oid . var OS : OidSet .
    var N : Natural .
    var CONF : Configuration .

    eq inv(S:[State]) = true .

    eq initCond({N, OS, CONF}) = noDupl(OS) .

    op noDupl : OidSet -> Bool .
    eq noDupl(O ; O ; OS) = false .
endm
