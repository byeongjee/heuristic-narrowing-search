fmod VALUE is
    protecting FVP-BOOL .
    protecting FVP-NAT .

    sorts Value InitValue? Value? .
    subsort  Value < Value? .
    subsort InitValue? < Value? .

    ops v1 v2 : -> Value [ctor] .

    op nil : -> InitValue? [ctor] .
endfm


fmod STEP is
    sorts Step InitStep .
    subsort InitStep < Step .
    op init : -> InitStep [ctor] .
    ops wait done : -> Step [ctor] .
endfm


fmod NODE is
    sorts Node Cid  .
    subsort Node < Cid .
    op Node : -> Cid [ctor] .
endfm


fmod STATUS is
    protecting FVP-NAT .
    protecting STEP .

    sorts Status InitStatus .
    subsort InitStatus < Status .
    sort InitialRound .

    op [_,_] : Natural Step -> Status [ctor format (d d d s d d)] . 
    op [_,_] : Natural InitStep -> InitStatus [ctor ditto] . 
endfm

fmod VOTE-MAP is
    protecting FVP-NAT .
    protecting VALUE .

    sorts VoteMap VoteMapEntry .
    subsort VoteMapEntry < VoteMap .

    op  _|->_ : Value Natural -> VoteMapEntry [ctor prec 50] .

    op empty : -> VoteMap [ctor] .
    op _;_ : VoteMap VoteMap -> VoteMap [ctor assoc comm id: empty prec 60] .
endfm

fmod VOTE is
    protecting VOTE-MAP .

    sort Vote .
    op [_,_] : VoteMap Natural -> Vote [ctor prec 70] .

    op update : Vote Value -> Vote .

    vars N NA N1 N2 TH : Natural .
    var VMAP : VoteMap .

    eq update([v1 |-> N ; VMAP, NA], v1) = [v1 |-> s N ; VMAP, s NA] [variant] .  
    eq update([v2 |-> N ; VMAP, NA], v2) = [v2 |-> s N ; VMAP, s NA] [variant] .  

    op genConst : Value Vote Natural -> Boolean .

    eq genConst(v1, [v1 |-> N1 ; v2 |-> N2, NA], TH) 
    = (N1 < TH) and (TH <= NA) and (N2 <= N1) [variant] .

    eq genConst(v2, [v1 |-> N1 ; v2 |-> N2, NA], TH) 
    = (N2 < TH) and (TH <= NA) and (N1 < N2) [variant] .

    sort InitVote .
    subsort InitVote < Vote .

    op initVote : -> InitVote .
    eq initVote = [v1 |-> 0 ; v2 |-> 0, 0] [variant] .
endfm


fmod OID-SET is
    sorts Oid OidSet .
    subsort Oid < OidSet .
    op none : -> OidSet [ctor] .
    op _;_ : OidSet OidSet -> OidSet [ctor assoc comm id: none] .
endfm
