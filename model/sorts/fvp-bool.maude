
set include BOOL off . 

fmod FVP-BOOL is
  sorts Boolean False .
  subsort False < Boolean .
  op false : -> False [ctor] .
  ops true : -> Boolean [ctor] .
  op not_ : Boolean -> Boolean .
  op _and_ : Boolean Boolean -> Boolean [assoc comm] .
  op _xor_ : Boolean Boolean -> Boolean [assoc comm] .
  op _or_ : Boolean Boolean -> Boolean .

  vars X Y : Boolean .

  eq X and true = X [variant] .
  eq X and false = false [variant] . 
  eq X and X = X [variant].
  eq X and X and Y = X and Y [variant] .  --- AC extension
  eq X xor false = X [variant] .
  eq X xor X = false [variant] .  --- AC extension
  eq X xor X xor Y = Y [variant] .
  eq not X = X xor true [variant] .
  eq X or Y = (X and Y) xor X xor Y [variant] . 
endfm