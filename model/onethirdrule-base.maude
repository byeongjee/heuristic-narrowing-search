
load ./sorts/fvp-bool.maude
load ./sorts/fvp-nat.maude
load ./sorts/sorts.maude

set include BOOL off .


mod ALT-CFIGURATION is
    protecting NODE .
    protecting INITIAL-SORTS .

    sorts None Object Msg ObjectSet Configuration InitObject InitConfiguration .
    subsort InitObject < Object InitConfiguration < Configuration .
    subsort Msg < Configuration .

    op none : -> InitConfiguration [ctor] .
    op __ : InitConfiguration InitConfiguration -> InitConfiguration [ctor assoc comm id: none] .
    op __ : Configuration Configuration -> Configuration [ctor assoc comm id: none] .

    op <_:_|_> : Oid Cid IU1234S -> InitObject [ctor object] .
    op <_:_|_> : Oid Cid U1234S -> Object [ctor object] .
endm


fmod MESSAGE-CONTENT is
    protecting FVP-NAT .
    protecting VALUE .

    sort MsgContent . 
    op vote : Value Natural -> MsgContent [ctor] .
endfm


mod MULTICAST is 
  including ALT-CFIGURATION .
  including MESSAGE-CONTENT .

  op multicast_from_to_ : MsgContent Oid OidSet -> Msg [ctor] .

  var MC : MsgContent . 
  var SENDER : Oid .

  eq multicast MC from SENDER to none = none [variant] .
  eq multicast MC from SENDER to none = none .
endm


mod GLOBAL is
    protecting ALT-CFIGURATION .
    protecting FVP-NAT .

    sort Global .
    op {_,_,_} : Natural OidSet Configuration -> Global [ctor format (d d d s d s d d)] .
    op decide? :  Value Boolean Natural OidSet Object Configuration -> Global [ctor] .
    op update? :  Value Boolean Natural OidSet Object Configuration -> Global [ctor] .
endm


mod ONETHIRDRULE-CONST is
    protecting ALT-CFIGURATION .
    protecting SET{Natural} .
    protecting SET{VoteMap} .
    protecting BOOL .

    vars N N' R N1 N2 N3 : Natural . 
    vars V V1 V2 V3 : Value .
    var Z : FZero .  
    var S : Step .
    var CF : Configuration . 
    var MSG : Msg . 
    var O : Oid . 
    var OS : OidSet .
    var NS : Set{Natural} .
    var VMAP : VoteMap .
    var VMAPS : Set{VoteMap} .

    op noDupl : OidSet -> Bool .
    eq noDupl(O ; O ; OS) = false .

    op sameRound : Set{Natural} -> Bool .
    eq sameRound((R, s R N, NS)) = false .
    eq sameRound((R, R, NS)) = sameRound((R, NS)) .
    eq sameRound(R) = true .
    eq sameRound(empty) = true .

    op getOids : Configuration -> OidSet [memo] .
    eq getOids(< O : Node | ATTS:U1234S > CF) = O ; getOids(CF) .
    eq getOids(MSG CF) =  getOids(CF) .
    eq getOids(none) =  none .

    op numObjs : Configuration -> Natural .
    eq numObjs(< O : Node | ATTS:U1234S > CF) = s numObjs(CF) .
    eq numObjs(MSG CF) = numObjs(CF) .
    eq numObjs(none) = 0 .

    op getRounds : Configuration -> Set{Natural} .
    eq getRounds(< O : Node | status : [R, S], ATTS:U234S > CF) = R, getRounds(CF) .
    eq getRounds(MSG CF) = getRounds(CF) .
    eq getRounds(none) = 0 .

    op valid1/2 : Natural Natural -> Bool .
    eq valid1/2(s s N', s N) = valid1/2(N', N) .
    eq valid1/2(N, Z) = false .
    eq valid1/2(s N, s N N') = true .

    op twoVoteMapEntry : Configuration -> Bool .

    op getVoteMaps : Configuration -> Set{VoteMap} .
    eq getVoteMaps(< O : Node | votes : [VMAP, N], ATTS:U123S > CF) 
    = VMAP, getVoteMaps(CF) .
    eq getVoteMaps(MSG CF) = getVoteMaps(CF) .
    eq getVoteMaps(none) = empty .

    op hasTwoEntries : Set{VoteMap} -> Bool .
    eq hasTwoEntries(((V1 |-> N1 ; V2 |-> N2 ; V3 |-> N3 ; VMAP), VMAPS)) = false .
    eq hasTwoEntries((V1 |-> N1, VMAPS)) = false .
    eq hasTwoEntries((V1 |-> N1 ; V2 |-> N2, VMAPS)) = hasTwoEntries(VMAPS) .
    eq hasTwoEntries((V |-> N1 ; V |-> N2 ; VMAP, VMAPS)) = false . --- no duplicate key allowed
    eq hasTwoEntries(empty) = true .

    eq twoVoteMapEntry(CF) = hasTwoEntries(getVoteMaps(CF)) .
endm
