load ./onethirdrule-base.maude


mod ONETHIRDRULE-NO-MSG-LOSS is
    including GLOBAL .
    including FUNCTIONS .
    protecting MULTICAST .

    vars O O' : Oid . vars CF : Configuration . var C : Cid .
    vars OS OS' : OidSet . 
    var R TH N VA : Natural .
    var VS : ValueSet .
    var Z : FZero .
    vars V V' : Value .
    var VM : VoteMap .
    var VOTE : Vote .

    rl [vote] : 
    {TH, VS, O ; OS,
        < O : Node | status : [R, init], value : V, decision : nil, votes : VOTE > 
        CF
    }
    =>
    {TH, VS, O ; OS,
        < O : Node | status : [R, wait], value : V, decision : nil, votes : update(VOTE, V) > 
        (multicast vote(V, R) from O to OS)
        CF
    } [narrowing] .


    rl [receive] :
    {TH, VS, OS,
        < O : Node | status : [R, wait], decision : nil, votes : VOTE, ATTS:U2S > 
        (multicast vote(V, R) from O' to (O ; OS'))
        CF
    } =>
    {TH, VS, OS,
        < O : Node | status : [R, wait], decision : nil, votes : update(VOTE, V), ATTS:U2S > 
        (multicast vote(V, R) from O' to OS')  
        CF
    } [narrowing] .


    rl [decide-1] :
    {TH, VS, OS,
        < O : Node | status : [R, wait], decision : nil, votes : [(V |-> N ; VM), VA], ATTS:U2S > 
        CF
    }
    =>
    decide?(V, TH <= N, TH, VS, OS, 
        < O : Node | status : [R, wait], decision : nil, votes : [(V |-> N ; VM), VA], ATTS:U2S >,
        CF
    ) [narrowing] .


    rl [decide-2] :
    decide?(V, true, TH, VS, OS,
        < O : Node | status : [R, wait], decision : nil, ATTS:U24S >,
        CF)
    =>
    {TH, VS, OS,
        < O : Node | status : [R, done], decision : V, ATTS:U24S >
        CF
    } [narrowing] .


    rl [update-1] : 
    {TH, V ; VS, OS,
        < O : Node | status : [R, wait], decision : nil, votes : [V |-> N ; VM, VA], ATTS:U2S >
        CF
    }
    =>
    update?(V, (N < TH) and (TH <= VA) and genConst(V, N, VM, VS),
        TH, V ; VS, OS,
        < O : Node | status : [R, wait], decision : nil, votes : [V |-> N ; VM, VA], ATTS:U2S >,
        CF
    ) [narrowing] .


    rl [update-2] : 
    update?(V', true, TH, VS, OS,
        < O : Node | status : [R, wait], value : V, decision : nil, votes : [VM, VA] >,
        CF
    )
    =>
    {TH, VS, OS,
        < O : Node | status : [s R, init], value : V', decision : nil, votes : [initVotes(VS), 0] >
        CF
    } [narrowing] .
endm


mod ONETHIRDRULE is
    including ONETHIRDRULE-NO-MSG-LOSS .
    vars TH NV : Natural .
    var OS : OidSet . 
    var CF : Configuration . 
    var MSG : Msg .
    var VS : ValueSet .

    rl [loss] : 
    {TH, VS, OS, MSG CF}
    =>
    {TH, VS, OS, CF} [narrowing] .
endm


mod ONETHIRDRULE-NML-ANALYSIS is
    protecting ONETHIRDRULE-NO-MSG-LOSS .
    protecting ONETHIRDRULE-CONST .
    including STATE .
    subsort Global < State .
    eq inv(S:State) = true .
endm


mod ONETHIRDRULE-ANALYSIS is
    protecting ONETHIRDRULE .
    protecting ONETHIRDRULE-CONST .
    including STATE .
    subsort Global < State .
    eq inv(S:State) = true .
endm
