load ../../heuristic-search/score.maude


fmod ONETHIRDRULE-SCORE1 is
    including SCORE .
    protecting INT .

    subsort Int < Score .

    vars N N' D : Int .
    vars T : Term .

    eq le(N, N') = N <= N' .

    eq score(T, D) = D .
endfm

--- for score2 and score3
fmod ONETHIRDRULE-SCORE-BASE is
    including SCORE .
    protecting INT .

    subsort Int < Score .

    vars N N' : Int .
    vars T BT NT OIDT OT CT ST : Term .
    var D : Nat .
    var NETL : NeTermList .

    eq le(N, N') = N <= N' .

    eq score(
        '`{_`,_`,_`}[T, OIDT, NETL], D
    ) = score(NETL) .

    op score : NeTermList -> Score .

    eq score('__[NETL]) = score(NETL) .
    eq score((T, NETL)) 
    = score(T) + score(NETL) .

    eq score(T) = 0 [owise] .

    *** auxiliary functions
    op toNumber : Term -> Int .
    eq toNumber('0.Nat) = 0 .
    eq toNumber('s.Nat) = 1 .
    eq toNumber(T) = 0 [owise] .
    eq toNumber('__[NETL]) = countS(NETL) .

    op countS : NeTermList -> Int .
    eq countS('s.Nat) = 1 .
    eq countS(T) = 0 [owise] .
    eq countS((T, NETL)) = countS(T) + countS(NETL) .
endfm

fmod ONETHIRDRULE-SCORE2 is
    including ONETHIRDRULE-SCORE-BASE .

    vars T VT BT NT OIDT OT CT ST : Term .
    var D : Nat .

    eq score(
        'decide?[VT, BT, NT, OIDT, OT, CT], D
    ) = score((OT, CT)) .
    eq score(
        'update?[VT, BT, NT, OIDT, OT, CT], D
    ) = score((OT, CT)) .

    var NETL : NeTermList .

    eq score('<_:_|_>[NETL]) =  1 . 

    eq score('multicast_from_to_[NETL]) = 0 .
endfm


fmod ONETHIRDRULE-SCORE3 is
    including ONETHIRDRULE-SCORE-BASE .

    vars T VT BT NT OIDT OT CT ST DT RT : Term .
    var D : Nat .

    eq score(
        'decide?[VT, BT, NT, OIDT, OT, CT], D
    ) = score((OT, CT)) .
    eq score(
        'update?[VT, BT, NT, OIDT, OT, CT], D
    ) = score((OT, CT)) .

    vars NETL NETL' : NeTermList .

    eq score('<_:_|_>[OT, CT, '_`,_[
        'status`:_['`[_`,_`][RT, ST]], NETL, 'decision`:_[DT], NETL']])
    = if DT == 'nil.InitialValue then 100 else 90 fi 
    + max(1, toNumber(RT)) .

    eq score('multicast_from_to_[NETL]) = 0 .
endfm


fmod ONETHIRDRULE-VU-NARROW-SCORE1 is
    including ONETHIRDRULE-SCORE1 .

    vars T BT : Term . var D : Nat .

    eq score('ready[T], D) = score(T, D) .
    eq score('check[BT, T], D) = score(T, D) .
endfm

fmod ONETHIRDRULE-VU-NARROW-SCORE2 is
    including ONETHIRDRULE-SCORE2 .

    vars T BT : Term . var D : Nat .

    eq score('ready[T], D) = score(T, D) .
    eq score('check[BT, T], D) = score(T, D) .
endfm 

fmod ONETHIRDRULE-VU-NARROW-SCORE3 is
    including ONETHIRDRULE-SCORE3 .

    vars T BT : Term . var D : Nat .

    eq score('ready[T], D) = score(T, D) .
    eq score('check[BT, T], D) = score(T, D) .
endfm 