fmod ONETHIRDRULE-INDEX-AUX is
    including META-TERM .

    vars T TN TO TV TB TOBJ TCF : Term .
    vars TL TL' : TermList .
    vars N N' : Nat .
    var F : Qid .
    var V : Variable .

    sort ConfigInfo .
    op #msgs_#objs_ : Nat Nat -> ConfigInfo [ctor] .

    op getInfo : TermList Nat Nat -> ConfigInfo .
    eq getInfo(('multicast_from_to_[TL'], TL), N, N') = getInfo(TL, s(N), N') .
    eq getInfo(('<_:_|_>[TL'], TL), N, N') = getInfo(TL, N, s(N')) .
    eq getInfo((V, TL), N, N') = getInfo(TL, N, N') .
    eq getInfo(empty, N, N') = #msgs N #objs N' .

    op getConfig : Term -> TermList .
    eq getConfig('decide?[TV, TB, TN, TO, TOBJ, TCF]) = TOBJ, getItems(TCF) .
    eq getConfig('update?[TV, TB, TN, TO, TOBJ, TCF]) = TOBJ, getItems(TCF) .
    eq getConfig('`{_`,_`,_`}[TN, TO, TCF]) = getItems(TCF) .

    op getItems : Term -> TermList .
    eq getItems('__[TL]) = TL .
    eq getItems(T) = T [owise] .
endfm


fmod ONETHIRDRULE-INDEX is
    including INDEX .
    including ONETHIRDRULE-INDEX-AUX .

    op index : Qid ConfigInfo -> Index [ctor] .

    var T : Term .
    var TL : TermList .
    var F : Qid .

    eq index(T) = index(getTopSymbol(T), getInfo(getConfig(T),0,0)) .

    op getTopSymbol : Term -> Qid .
    eq getTopSymbol(F[TL]) = F .
endfm


mod ONETHIRDRULE-VU-NARROW-INDEX is
    including INDEX .
    including ONETHIRDRULE-INDEX-AUX .

    op index : Qid Qid ConfigInfo -> Index [ctor] .

    vars TL : TermList .
    vars F F' : Qid .

    eq index(F[F'[TL]])
     = index(F, F', getInfo(getConfig(F[F'[TL]]),0,0)) .

endm
