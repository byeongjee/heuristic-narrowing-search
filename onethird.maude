load nat2.maude
load heuristic-narrowing.maude
load sorts.maude

set include BOOL off .


mod CONFIGURATION is
    protecting BOOL2 .
    protecting NAT2 .
    protecting VALUE .
    protecting NODE .
    protecting STATUS .
    protecting VOTE .
    protecting OID-SET .
    protecting INITIAL-SORTS .

    sorts Attribute AttributeSet InitialAttribute InitialAttributeSet .
    subsort Attribute < AttributeSet .
    subsort InitialAttributeSet < AttributeSet .

    sorts None Object Msg ObjectSet MsgSet Configuration InitialObject InitialObjectSet .

    subsort MsgSet ObjectSet < Configuration .
    subsort InitialObject < Object InitialObjectSet < ObjectSet .
    subsort None < ObjectSet MsgSet .
    subsort Msg < MsgSet .

    op <_:_|_> : Oid Cid IU12345S -> InitialObject [ctor object] .
    op <_:_|_> : Oid Cid U12345S -> Object [ctor object] .
    op none : -> None [ctor] .
    op __ : None None -> None [ctor config assoc comm id: none] .
    op __ : InitialObjectSet InitialObjectSet -> InitialObjectSet [ctor config assoc comm id: none] .
    op __ : ObjectSet ObjectSet -> ObjectSet [ctor config assoc comm id: none] .
    op __ : MsgSet MsgSet -> MsgSet [ctor config assoc comm id: none] .
    op __ : Configuration Configuration -> Configuration [ctor config assoc comm id: none] .

endm


fmod MESSAGE-CONTENT is
    protecting NAT2 .
    sort MsgContent . 

    ops trueVote falseVote : Nat2 -> MsgContent [ctor] .
endfm



mod MULTICAST is 
  including CONFIGURATION .
  including MESSAGE-CONTENT .

  op multicast_from_to_ : MsgContent Oid OidSet -> Msg [ctor] .
  var MC : MsgContent . var SENDER : Oid .
  eq multicast MC from SENDER to none = none [variant] .
endm

mod GLOBAL is
    protecting CONFIGURATION .
    protecting NAT2 .

    sort State .

    op {_,_} : Nat2 Configuration -> State [ctor] .
endm


mod ONETHIRDRULE is
    including GLOBAL .
    protecting NAT2 .
    protecting VALUE .
    protecting MULTICAST .
    protecting BOOL2 .
    protecting BOOL .

    vars O O' O'' : Oid . vars CONF REST : Configuration . vars C C' : Cid .
    var MSG : Msg .  var MC : MsgContent .
    vars NEIGHBORS OIDS : OidSet .
    var R : Nat2 .
    vars N N' REM REM' : Nat2 .
    vars V_TRUE V_FALSE V_TOT : Nat2 .
    vars STEP STEP' : Step .
    vars V D : Value .

    rl [loss] : 
    {N, MSG CONF}
    =>
    {N, CONF} [narrowing] .

    rl [vote-true] : 
    {N,
        < O : Node | status : (R, init), value : true, decision : nil, votes : (0, 0, 0), neighbors : NEIGHBORS > 
        CONF
    }
    =>
    {N,
        < O : Node | status : (R, waiting), value : true, decision : nil, votes : (s, 0, s), neighbors : NEIGHBORS > 
        (multicast trueVote(R) from O to NEIGHBORS)
        CONF
    } [narrowing] .

    rl [vote-false] : 
    {N,
        < O : Node | status : (R, init), value : false, decision : nil, votes : (0, 0, 0), neighbors : NEIGHBORS > 
        CONF
    }
    =>
    {N,
        < O : Node | status : (R, waiting), value : false, decision : nil, votes : (0, s, s), neighbors : NEIGHBORS > 
        (multicast falseVote(R) from O to NEIGHBORS)
        CONF
    } [narrowing] .


    rl [receive-true-vote] :
    {N,
        < O : Node | status : (R, waiting), decision : nil, votes : (V_TRUE, V_FALSE, V_TOT), ATTS:U25S > 
        (multicast trueVote(R) from O' to (O ; OIDS))
        CONF
    } =>
    {N,
        < O : Node | status : (R, waiting), decision : nil, votes : (s V_TRUE, V_FALSE, s V_TOT), ATTS:U25S > 
        (multicast trueVote(R) from O' to OIDS)  
        CONF
    } [narrowing] .

    rl [receive-false-vote] :
    {N,
        < O : Node | status : (R, waiting), decision : nil, votes : (V_TRUE, V_FALSE, V_TOT), ATTS:U25S > 
        (multicast falseVote(R) from O' to (O ; OIDS))
        CONF
    } =>
    {N,
        < O : Node | status : (R, waiting), decision : nil, votes : (V_TRUE, s V_FALSE, s V_TOT), ATTS:U25S > 
        (multicast falseVote(R) from O' to OIDS)  
        CONF
    } [narrowing] .


    op decideTrue : Bool2 Nat2 Object Configuration -> State [ctor] .
    op decideFalse : Bool2 Nat2 Object Configuration -> State [ctor] .
    op updateTrue : Bool2 Nat2 Object Configuration -> State [ctor] .
    op updateFalse : Bool2 Nat2 Object Configuration -> State [ctor] .

    op _<=_ : Nat2 Nat2 -> Bool2 .
    eq N <= N N' = true [variant] .

    rl [decideTrue-1] :
    {N,
        < O : Node | status : (R, waiting), decision : nil, votes : (V_TRUE, V_FALSE, V_TOT), ATTS:U25S > 
        CONF
    }
    =>
    decideTrue(N <= V_TRUE,
        N,
        < O : Node | status : (R, waiting), decision : nil, votes : (V_TRUE, V_FALSE, V_TOT), ATTS:U25S >,
        CONF
    ) [narrowing] .

    rl [decideTrue-2] :
    decideTrue(true,
        N,
        < O : Node | status : (R, waiting), decision : nil, ATTS:U245S >,
        CONF
    ) 
    =>
    {N,
        < O : Node | status : (R, finished), decision : true, ATTS:U245S  >
        CONF
    } [narrowing] .

    rl [decideFalse-1] :
    {N,
        < O : Node | status : (R, waiting), decision : nil, votes : (V_TRUE, V_FALSE, V_TOT), ATTS:U25S > 
        CONF
    }
    =>
    decideFalse(N <= V_FALSE,
        N,
        < O : Node | status : (R, waiting), decision : nil, votes : (V_TRUE, V_FALSE, V_TOT), ATTS:U25S >,
        CONF
    ) [narrowing] .

    rl [decideFalse-2] :
    decideFalse(true,
        N,
        < O : Node | status : (R, waiting), decision : nil, votes : (V_TRUE, V_FALSE, V_TOT), ATTS:U245S >,
        CONF)
    =>
    {N,
        < O : Node | status : (R, finished), decision : false, votes : (V_TRUE, V_FALSE, V_TOT), ATTS:U245S >
        CONF
    } [narrowing] .

    rl [updateTrue-1] : 
    {N,
        < O : Node | status : (R, waiting), decision : nil, votes : (V_TRUE, V_FALSE, V_TOT), ATTS:U25S >
        CONF
    }
    =>
    updateTrue(
        (s V_TRUE <=  N) and (N <= V_TOT) and (V_FALSE <= V_TRUE),
        N,
        < O : Node | status : (R, waiting), decision : nil, votes : (V_TRUE, V_FALSE, V_TOT), ATTS:U25S >,
        CONF
    ) [narrowing] .

    rl [updateTrue-2] : 
    updateTrue(
        true,
        N,
        < O : Node | status : (R, waiting), value : V, decision : nil, votes : (V_TRUE, V_FALSE, V_TOT), ATTS:U5S >,
        CONF
    )
    =>
    {N,
        < O : Node | status : (s R, init), value : true, decision : nil, votes : (0, 0, 0), ATTS:U5S >
        CONF
    } [narrowing] .

    rl [updateFalse-1] : 
    {N,
        < O : Node | status : (R, waiting), decision : nil, votes : (V_TRUE, V_FALSE, V_TOT), ATTS:U25S >
        CONF
    }
    =>
    updateFalse(
        (s V_FALSE <= N) and (N <= V_TOT) and (s V_TRUE <= V_FALSE),
        N,
        < O : Node | status : (R, waiting), decision : nil, votes : (V_TRUE, V_FALSE, V_TOT), ATTS:U25S >,
        CONF
    ) [narrowing] .

    rl [updateFalse-2] : 
    updateFalse(
        true,
        N,
        < O : Node | status : (R, waiting), value : V, decision : nil, votes : (V_TRUE, V_FALSE, V_TOT), ATTS:U5S >,
        CONF
    )
    =>
    {N,
        < O : Node | status : (s R, init), value : false, decision : nil, votes : (0, 0, 0), ATTS:U5S >
        CONF
    } [narrowing] .





    var S : State . var M : Msg .
    var SS : [State] .

    op invariant : State -> Bool .
    eq invariant(SS) = true .

    op initialInvariant : State -> Bool [memo] .
    eq initialInvariant({N, CONF}) 
    = noDuplicateInNeighbor(getNeighborSet(CONF)) 
    and noDuplicate(getOids(CONF)) 
    and isThresholdValid(getOids(CONF), N) .




    sort NeighborSet .
    op _:_ : Oid OidSet -> NeighborSet .
    op emptyNeighborSet : -> NeighborSet .
    op _,_ : NeighborSet NeighborSet -> NeighborSet [ctor assoc comm id: emptyNeighborSet] .
    op getNeighborSet : Configuration -> NeighborSet [memo] .
    eq getNeighborSet(
        < O : Node | neighbors : NEIGHBORS, ATTS:U1234S >
        CONF
    ) = (O : NEIGHBORS), getNeighborSet(CONF) .
    eq getNeighborSet(M CONF) = getNeighborSet(CONF) .
    eq getNeighborSet(none) = emptyNeighborSet .

    var NEIGHBOR_SET : [NeighborSet] .

    op noDuplicateInNeighbor : NeighborSet -> Bool [memo] .
    eq noDuplicateInNeighbor((O : (O' ; O' ; NEIGHBORS)), NEIGHBOR_SET) = false .
    eq noDuplicateInNeighbor((O : (O ; NEIGHBORS)), NEIGHBOR_SET) = false .

    op getOids : Configuration -> OidSet [memo] .

    eq getOids(< O : C | ATTS:U12345S > CONF) = O ; getOids(CONF) .
    eq getOids(M CONF) =  getOids(CONF) .
    eq getOids(none) =  none .

    op noDuplicate : OidSet -> Bool [memo] .
    eq noDuplicate(O ; O ; OIDS) = false .

    var Z : Zero2 .

    op isThresholdValid : OidSet Nat2 -> Bool [memo] .
    eq isThresholdValid((O ; O' ; OIDS), s N) = isThresholdValid(OIDS, N) .
    eq isThresholdValid(OIDS, Z) = false .
    eq isThresholdValid(none, s N) = true .
***(
    eq isThresholdValid((O ; O' ; O'' ; OIDS), s s N) = isThresholdValid(OIDS, N) .
    eq isThresholdValid((O ; O' ; O'' ; OIDS), s Z) = false .
    eq isThresholdValid(OIDS, Z) = false .
    eq isThresholdValid(none, s N) = true .
)

endm


mod ONETHIRDRULE-TEST is
    including ONETHIRDRULE .
    protecting BOUNDED-NARROWING .

    vars O O' O'' O1 O2 O3 O4 : Oid .
    vars C1 C2 C3 C4 : Cid .
    vars OIDS ALL : OidSet .
    vars R R' : Nat2 .
    vars V V' : Value .
    vars N N' : Nat2 .
    vars V_TRUE V_FALSE V_TOT  : Nat2 .
    vars V_TRUE' V_FALSE' V_TOT' : Nat2 .
    vars NEIGHBORS NEIGHBORS' : OidSet .
    var INITIAL : InitialObjectSet .
    vars STEP STEP' : Step .
    var CONF : Configuration .

    ops a b c d : -> Oid .

endm


***(

search [1]
    {s s, 
    < a : Node | status : 0, value : false, step : init, decision : nil, votes : 0, 0 / 0, neighbors : b ; c >
    < b : Node | status : 0, value : false, step : init, decision : nil, votes : 0, 0 / 0, neighbors : a ; c >
    < c : Node | status : 0, value : true, step : init, decision : nil, votes : 0, 0 / 0, neighbors : a ; b >
    } =>* 
{s s,
    < O : Node | status : R, value : V, step : STEP, decision : true, votes : V_TRUE, V_FALSE / V_TOT, neighbors : NEIGHBORS >
    < O' : Node | status : R', value : V', step : STEP', decision : false, votes : V_TRUE', V_FALSE' / V_TOT', neighbors : NEIGHBORS' >
    CONF
    } .
)

***(
{fold} vu-narrow
{N, INITIAL} =>*
{
    N,
    < O : Node | status : R, value : V, step : STEP, decision : true, votes : V_TRUE, V_FALSE / V_TOT, neighbors : NEIGHBORS >
    < O' : Node | status : R' , value : V', step : STEP', decision : false, votes : V_TRUE', V_FALSE' / V_TOT', neighbors : NEIGHBORS' >
    CONF
} .
)


set print attribute on .

red metaHeuristicNarrowingSearch(
    upModule('ONETHIRDRULE-TEST, false),
    upTerm({N, INITIAL}),
    upTerm({N',
    < O : Node | decision : true, ATTS:U1245S >
    < O' : Node | decision : false, ATTS':U1245S >
    CONF}),
    delay filter) .
